buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        // AWS core for getting profile credentials
        classpath "com.amazonaws:aws-java-sdk-core:1.10.+"
    }
}

plugins {
    id 'net.saliman.cobertura' version '2.2.7'
}

ext {
    try {
        // Let the Amazon SDK load the credentials - this will work with
        // both user profile credentials and instance profile credentials (e.g. for Jenkins)
        def credentials = new com.amazonaws.auth.DefaultAWSCredentialsProviderChain().credentials
        if(credentials) {
            aws = [
                    accessKey: credentials.getAWSAccessKeyId(),
                    secretKey: credentials.getAWSSecretKey()
            ]
        }
    } catch(Exception e) {
        logger.warn "Could not determine AWS credentials: ${e.message}"
        aws = [
                accessKey: null,
                secretKey: null
        ]
    }
}

repositories {
    mavenCentral()

    // Our own private S3 repository
    maven {
        name "spiideoS3"
        url "s3://spiideo-artifacts/"
        credentials(AwsCredentials) {
            accessKey aws.accessKey
            secretKey aws.secretKey
        }
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven-publish'

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

group 'com.iheartradio.m3u8'
archivesBaseName = 'open-m3u8'
sourceCompatibility = 1.7
version "0.2.6+${System.currentTimeSeconds()}"

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testRuntime "org.slf4j:slf4j-log4j12:1.7.5"
    testRuntime "log4j:log4j:1.2.7"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    // Publish artifacts with Maven
    publications {
        maven(MavenPublication) {
            from components.java
            // The source JAR
            artifact sourcesJar {
                classifier "sources"
            }
        }
    }
    // Publish to our own S3 repository
    repositories {
        add project.repositories.spiideoS3
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "2.8"
}
